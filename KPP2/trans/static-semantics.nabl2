module static-semantics

imports

  signatures/-
  

signature

  sorts
    Object

  namespaces
    Module
    Class
    Identifier
  
  name resolution
    labels P T
  
  constructors
    Number : Type
    TemplateAddQuotes : String -> Term
    TemplateConcat : String * String -> Term
    TemplateVictim : Term
    TemplateProperty : Term
    String : Type
    Class : Type * scope * Declaration -> Type
    Object : Class * scope * Declaration -> Type
    Dynamic : Type
    Func : list(Type) * Type -> Type
    Coerce : Type -> Type
    Ref : Type * Term -> Type
    Unit : Type
    Instruction : Type -> Type

  constraint generator
  
    [[ Expr ^ (scope) : Type ]]

  functions
  
    getScope : (Term) -> scope {
    	  Object(_, scope, _) -> scope,
    	  Class(_, scope, _) -> scope
    }

rules

  init ^ (topScope) :=
    Number() <! Dynamic(),
    new topScope.

  [[ ModernFile(File(usings, classes)) ^ (topScope) ]] := 
    Type { DynamicT() } <- topScope,
    Type { DynamicT() } : Dynamic(),
    
    Type { NumberT() } <- topScope,
    Type { NumberT() } : Number(),
    Number() <! Dynamic(),
    
    Map1 [[ classes ^ (topScope) ]].
    
  [[ LegacyFile(_) ^ (topScope) ]] := false | note "No support for legacy files".

  [[ Class(className, definitions) ^ (topScope) ]] :=
    distinct/name D(topScope)/Identifier | note $[Duplicate identifier 1 [NAME]] @NAMES,
  	new classScope,
  	classScope ---> topScope,
    new instanceScope1,
    instanceScope1 ---> classScope,
  	
  	Identifier { className } <- topScope,
  	Identifier { className } : Class(className, classScope, Identifier { className }),
  	Identifier { className }.accessor := KArrayIndex(TemplateVictim(), KString(TemplateAddQuotes(TemplateProperty()))),
  	
  	definition == Identifier { className },
  	Identifier { "new" } <- classScope,
  	Identifier { "new" } : Func(Object(className, instanceScope1, definition), []),
  	Type { CustomT(className) } <- topScope,
  	Type { CustomT(className) } : Object(className, instanceScope1, definition),
  	
  	Object(className, instanceScope1, definition) <! Dynamic(),
  	Func(Object(className, instanceScope1, definition), []) <! Dynamic(),
  	
    Map2 [[ definitions ^ (Object(className, instanceScope1, definition), classScope) ]].
  
  [[ Method(methodType, methodName, parameters, methodInstructions) ^ (objectType, classScope) ]] :=
    distinct/name D(instanceScope2)/Identifier | note $[Duplicate identifier 2 [NAME]] @NAMES,
    objectType == Object(_, instanceScope2, _),
    //objectType == instanceScope2,
    
    Identifier { methodName } <- instanceScope2,
    Identifier { methodName } : Func(paramTypes, methodType),
    
    StripParameters[[ parameters ^ () : paramTypes ]],
    Func(paramTypes, methodType) <! Dynamic(),

    new methodScope,
    methodScope ---> classScope,
    
    Identifier { "this" } <- methodScope,
    Identifier { "this" } : objectType,
    
    Map1 [[ methodInstructions ^ (methodScope) ]].
  
  wrapInstruction [[ any ^ () : Instruction(any) ]].
  
  StripParameters [[ [] ^ () : [] ]].
  StripParameters [[ [elem | rest] ^ () : [eType | restTypes] ]] :=
  	StripParameters [[ elem ^ () : eType ]],
  	StripParameters [[ rest ^ () : restTypes ]].

  StripParameters [[ Parameter(type, _) ^ () : type ]].
    
  [[ Field(typeLiteral, fieldName, fieldExpr) ^ (objectType, classScope) ]] :=
    distinct/name D(instanceScope3)/Identifier | note $[Duplicate identifier 3 [NAME]] @NAMES,
    objectType == Object(_, instanceScope3, _),
    
    new fieldScope, fieldScope ---> classScope,
    [[ fieldExpr ^ (fieldScope) : exprType ]],
    
    typeLookup [[ typeLiteral ^ (fieldScope) : fieldType ]],
    exprType <? fieldType,
    
    Identifier { fieldName } <- instanceScope3,
    Identifier { fieldName } : Ref(fieldType, Identifier { fieldName }),
    //Identifier { fieldName }.accessor := KArrayIndex(TemplateVictim(), AddQuotes(fieldName)),
    //Identifier { fieldName }.location := KArrayIndex(KIdentifier("this"), AddQuotes(fieldName)),
    Ref(fieldType, Identifier { fieldName }) <! fieldType.
    
  
  [[ Declaration(typeLiteral, varName) ^ (methodScope) ]] :=
    distinct/name D(methodScope)/Variable | note "Duplicate identifier - foo42" @NAMES,
    Identifier { varName } <- methodScope,
    Identifier { varName } : Ref(varType),
    Ref(varType) <! varType,
    Identifier { varName } genOf varType,
    typeLookup [[ typeLiteral ^ (methodScope) : varType ]].
  
  [[ Assignment(victim, valueExpr) ^ (methodScope) ]] :=
    [[ victim ^ (methodScope) : victimType ]],
    victimType == Ref(refType, _) | note "Not a writable reference",
    [[ valueExpr ^ (methodScope) : valueType ]],
    valueType <? refType.
  
  [[ DeclarationAssigned(typeLiteral, varName, valueExpr) ^ (methodScope) ]] :=
    distinct/name D(methodScope)/Variable | note "Duplicate identifier 3" @NAMES,
    Identifier { varName } <- methodScope,
    Identifier { varName } : Ref(varType),
    [[ valueExpr ^ (methodScope) : valType ]],
    typeLookup [[ typeLiteral ^ (methodScope) : varType ]],
    valType <? varType.
  
  //[[ Ref(Identifier(varName)) ^ (scope) : identifierRefType ]] :=
  //  getProperty [[ varName ^ (scope) : identifierRefType ]].
  
  [[ Identifier(varName) ^ (scope) : identifierType ]] :=
    getProperty [[ varName ^ (scope) : identifierType ]].
  
  [[ Accessor(victim, _, propertyName) ^ (scope) : propertyType ]] :=
    
    [[ victim ^ (scope) : victimType ]],
    victimScope is getScope of victimType,
    new s_use, s_use ---> victimScope,
    getProperty [[ propertyName ^ (s_use) : propertyType ]].

  // TODO: check function arguments
  [[ FunctionCall(functor, _) ^ (scope) : outType ]] :=
    [[ functor ^ (scope) : Func(outType, _) ]].
 
  getProperty [[ string ^ (scope) : propertyType2 ]] :=
    Identifier { string } -> scope,
    Identifier { string } |-> declaration,
    declaration : propertyType2 !.
  
  //fetchScope [[ object ^ (scope) : 43 ]] :=
  //	[[ object ^ (scope) : _ ]].
  //getScope [[ object ^ (scope) : embeddedObjectScope ]] :=
  //	[[ object ^ (scope) : objectScope ]],
  //	objectScope == Object(_, embeddedObjectScope).
  	
  //getScope [[ object ^ (scope) : embeddedClassScope ]] :=
  //	[[ object ^ (scope) : Class(_, embeddedClassScope) ]].
  
  [[ ExprInstruction(a) ^ (scope) ]] :=
  	[[ a ^ (scope) : _ ]].
   
  [[ Using(_) ^ (topScope) ]] := false.
  
  [[ LegacyBlock(_) ^ (_) ]].
  [[ Cast(typeLiteral, expr) ^ (scope) : type ]] :=
  	[[ expr ^ (scope) : _ ]],
  	typeLookup [[ typeLiteral ^ () : type ]].
  	
  [[ Add(a, b) ^ (scope) : Number() ]] :=
    [[ a ^ (scope) : aType ]],
    [[ b ^ (scope) : bType ]],
    aType <? Number(),
    bType <? Number().
  
  [[ NumberLiteral(_) ^ (_) : Number() ]].
  [[ StringLiteral(_) ^ (_) : String() ]].
  
  typeLookup [[ any ^ (s) : lookupType ]] :=
    Type { any } -> s,
    Type { any } |-> declaration,
    declaration : lookupType !.

rules // instructions

  